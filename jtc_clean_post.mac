//******************************************************
//JTC　特許協力条約（見解書）翻訳ファイル後処理
//2012.04.12 created
//訳文(_eng.txt)上で、翻訳後ワード出力前に実行する
//******************************************************

//メッセージカウンタ
#count = 0;

//一括UNDO印
begingroupundo;

//、をカンマに変換

	setcompatiblemode 0x0F;
	replaceall "、" , ", " , regular;
	if( result ){
		call AddMsg "1:OK 「、」をカンマ「, 」に置換しました。\n";
	}
	if( ! result ){
		call AddMsg "1:NA 「、」なし。\n";
	}


//スペース修正。全角スペース、半角スペース２つをスペース１つに置換。
//※ピリオド後もスペース１つにすること！（２００９年１２月のネイティブチェックより。）

	setcompatiblemode 0x0F;
	replaceall "  |　" , " " , regular;
	if( result ){
		call AddMsg "2:OK 「　」、[][]２つを[]１つに置換しました。\n";
	}
	if( ! result ){
		call AddMsg "2:NA 「　」、[][]２つなし。\n";
	}

//文頭にタブを挿入
//タブが入っていなくても１文ずつ適宜挿入すること（２００９年１２月のネイティブチェックより。）

	setcompatiblemode 0x0F;
	replaceall "^\\f[^(\\n|IV|V|VII|VIII|・|JP|\\[)]+" , "\\0\\t\\1" , regular, casesense;
	if( result ){
		call AddMsg "3:OK 文頭にタブを挿入しました。\n";
	}
	if( ! result ){
		call AddMsg "3:NA タブ挿入失敗。\n";
	}

//上記処理で、In addition, In view of the foregoing, 等Iで始まる文が除外されてしまうので、修正
	setcompatiblemode 0x0F;
	replaceall "^\\f(In|It)" , "\\0\\t\\1" , regular, casesense;

//以下のパターンの文頭タブは削除
	setcompatiblemode 0x0F;
	replaceall "\\t\\fClaim [0-9]+\\n" , "\\1" , regular, casesense;

	setcompatiblemode 0x0F;
	replaceall "\\t\\fClaims [0-9|\\-|(and)|\\,| ]+\\n" , "\\1" , regular, casesense;

//claims#修正
//例)claims 1-2, 4-7 of this application
	replaceall "[0-9|\\-| ]+\\f,\\f[0-9|\\-| ]+(\\n|, which does|, which do|of|does|lacks|involves|is|\\]|\\)|>)" , "\\0 and\\2" , regular;

	if( result ){
		call AddMsg "4:OK claims#修正しました。\n";
	}
	if( ! result ){
		call AddMsg "4:NA claims#修正失敗。\n";
	}


//documents#修正
	replaceall "[0-9|\\-| ]+\\f,\\f[0-9|\\-| ]+(cited|\\.)" , "\\0 and\\2" , regular;

	if( result ){
		call AddMsg "5:OK documents#修正しました。\n";
	}
	if( ! result ){
		call AddMsg "5:NA documents#修正失敗。\n";
	}

//document 1, document 2 and document 4→documents 1, 2 and 4

	replaceall "of document \\f[0-9]+\\f, document \\f[0-9]+\\f and document \\f[0-9]+" , "of documents \\1, \\3 and \\5" , regular;

//document 1 and document 2→documents 1 and 2

	replaceall "of document \\f[0-9]+\\f and document \\f[0-9]+" , "of documents \\1 and \\3" , regular;

//Document 1 and document 2→Documents 1 and 2

	replaceall "Document \\f[0-9]+\\f and document \\f[0-9]+" , "Documents \\1 and \\3" , regular, casesense;


//例）documents 1-2. 例）documents 1-2 cited in the ISR　→documents 1 and 2, documents 9-10, 11-12なども対応
gofiletop;

while (true) {
	searchdown "documents \\f[0-9]+\\f\\-\\f[0-9]+\\f( cited in the ISR|\\.[^0-9]+)", regular;
	if(!result) break;
	wordright;
	beginsel;
	right 5;
	copy;
	beginclipboardread;
	$a[0] = getclipboard;
	#dash = strstr($a[0], "-");
	//最初の数字が0-9の場合、dashのindexは1
	if(#dash == 1){
		//さらに、最初の数字が9で、かつ次の数字が10の場合も対応
		//例）documents 9-10, 1-20, 1-2 
		$str1 = midstr($a[0], 0, 1);
		#num1 = val($str1);
		//次の数字２ケタ分をまず取得する
		$str2 = midstr($a[0], 2, 2);
		//5.など、ピリオドが小数点として認識される場合を除く
		#period = strstr($str2, ".");
		if (#period == 1){
			$str2 = midstr($a[0], 2, 1);
			#num2 = val($str2);
		}
		else{
			#num2 = val($str2);
			if(#num2 == 0){
				//数値で取れなかったら再度１ケタ分を取得する
				$str2 = midstr($a[0], 2, 1);
			}
		}
	}
	//最初の数字が２ケタの場合、dashのindexは2
	else if(#dash == 2){
		$str1 = midstr($a[0], 0, 2);
		#num1 = val($str1);
		//次の数字２ケタ分を取得する
		$str2 = midstr($a[0], 3, 2);
		#num2 = val($str2);
	}
//	message "$str1, $str2 =" + $str1 + " , " + $str2;
	if((#num1+1) == #num2){
		left 5;
		selectline; //今検索した範囲のみ置換範囲とする
		replacedown $str1 + "-" + $str2, $str1 + " and " + $str2;
	}
}

clearcliphist;

//fig. 1-2は、fig. 1 and 2に修正, fig. 9-10なども対応
gofiletop;

while (true) {
	searchdown "fig\\. \\f[0-9]+\\f\\-\\f[0-9]+", regular;
	if(!result) break;
	wordright;
	beginsel;
	right 5;
	copy;
	beginclipboardread;
	$a[0] = getclipboard;
	#dash = strstr($a[0], "-");
	//最初の数字が0-9の場合、dashのindexは1
	if(#dash == 1){
		//さらに、最初の数字が9で、かつ次の数字が10の場合も対応
		//例）fig. 9-10, 1-20, 1-2 
		$str1 = midstr($a[0], 0, 1);
		#num1 = val($str1);
		//次の数字２ケタ分をまず取得する
		$str2 = midstr($a[0], 2, 2);
		//5.など、ピリオドが小数点として認識される場合を除く
		#period = strstr($str2, ".");
		if (#period == 1){
			$str2 = midstr($a[0], 2, 1);
			#num2 = val($str2);
		}
		else{
			#num2 = val($str2);
			if(#num2 == 0){
				//数値で取れなかったら再度１ケタ分を取得する
				$str2 = midstr($a[0], 2, 1);
			}
		}
	}
	//最初の数字が２ケタの場合、dashのindexは2
	else if(#dash == 2){
		$str1 = midstr($a[0], 0, 2);
		#num1 = val($str1);
		//次の数字２ケタ分を取得する
		$str2 = midstr($a[0], 3, 2);
		#num2 = val($str2);
	}
//	message "$str1, $str2 =" + $str1 + " , " + $str2;
	if((#num1+1) == #num2){
		left 5;
		selectline; //今検索した範囲のみ置換範囲とする
		replacedown $str1 + "-" + $str2, $str1 + " and " + $str2;
	}
}

//リファレンス番号に（）をつける。例）a breather pipe (81)
// [0-9]+[a-Z]? ?
//page #, pages, line, lines, fig., claim, claims, document, documents, invention, inventions, Invention, Inventions, claims 1, 2, 5(前が数字または、カンマの場合も除く)claim 1, 3, 5 or 6, claims 1, 3-5, 7-8, 10, example, examples, and, to, is, WO , JP, January からDecember, Rule, Box V.からVIII, than, of, over, less,(直前のカンマを省くover 2 and 10 or less, 2-10 fold), to be, table, tables, fig. 18, 19, claims 1-6, 36, wherein, 

// [0-9]+[^("|th|,)]?("| |\)|,)



//一括UNDO印
endgroupundo;

//完了メッセージ作成
$complete = "";

#i = #count;
while(#count >= 0){
	$complete = $complete + $msg[#i-#count];
	#count = #count - 1;
}

//完了メッセージ表示
message $complete;

endmacro;

//***
//処理内容メッセージを追加するサブルーチン
//***
AddMsg:
	$msg[#count] = $$1;
	#count = #count + 1;
	return;